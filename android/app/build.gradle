apply plugin: "com.android.application"
apply plugin: 'io.fabric'

import com.android.build.OutputFile

/**
 * The react.gradle file registers a task for each build variant (e.g. bundleDebugJsAndAssets
 * and bundleReleaseJsAndAssets).
 * These basically call `react-native bundle` with the correct arguments during the Android build
 * cycle. By default, bundleDebugJsAndAssets is skipped, as in debug/dev mode we prefer to load the
 * bundle directly from the development server. Below you can see all the possible configurations
 * and their defaults. If you decide to add a configuration block, make sure to add it before the
 * `apply from: "../../node_modules/react-native/react.gradle"` line.
 *
 * project.ext.react = [
 *   // the name of the generated asset file containing your JS bundle
 *   bundleAssetName: "index.android.bundle",
 *
 *   // the entry file for bundle generation
 *   entryFile: "index.android.js",
 *
 *   // whether to bundle JS and assets in debug mode
 *   bundleInDebug: false,
 *
 *   // whether to bundle JS and assets in release mode
 *   bundleInRelease: true,
 *
 *   // whether to bundle JS and assets in another build variant (if configured).
 *   // See http://tools.android.com/tech-docs/new-build-system/user-guide#TOC-Build-Variants
 *   // The configuration property can be in the following formats
 *   //         'bundleIn${productFlavor}${buildType}'
 *   //         'bundleIn${buildType}'
 *   // bundleInFreeDebug: true,
 *   // bundleInPaidRelease: true,
 *   // bundleInBeta: true,
 *
 *   // whether to disable dev mode in custom build variants (by default only disabled in release)
 *   // for example: to disable dev mode in the staging build type (if configured)
 *   devDisabledInStaging: true,
 *   // The configuration property can be in the following formats
 *   //         'devDisabledIn${productFlavor}${buildType}'
 *   //         'devDisabledIn${buildType}'
 *
 *   // the root of your project, i.e. where "package.json" lives
 *   root: "../../",
 *
 *   // where to put the JS bundle asset in debug mode
 *   jsBundleDirDebug: "$buildDir/intermediates/assets/debug",
 *
 *   // where to put the JS bundle asset in release mode
 *   jsBundleDirRelease: "$buildDir/intermediates/assets/release",
 *
 *   // where to put drawable resources / React Native assets, e.g. the ones you use via
 *   // require('./image.png')), in debug mode
 *   resourcesDirDebug: "$buildDir/intermediates/res/merged/debug",
 *
 *   // where to put drawable resources / React Native assets, e.g. the ones you use via
 *   // require('./image.png')), in release mode
 *   resourcesDirRelease: "$buildDir/intermediates/res/merged/release",
 *
 *   // by default the gradle tasks are skipped if none of the JS files or assets change; this means
 *   // that we don't look at files in android/ or ios/ to determine whether the tasks are up to
 *   // date; if you have any other folders that you want to ignore for performance reasons (gradle
 *   // indexes the entire tree), add them here. Alternatively, if you have JS files in android/
 *   // for example, you might want to remove it from here.
 *   inputExcludes: ["android/**", "ios/**"],
 *
 *   // override which node gets called and with what additional arguments
 *   nodeExecutableAndArgs: ["node"],
 *
 *   // supply additional arguments to the packager
 *   extraPackagerArgs: []
 * ]
 */

project.ext.react = [
  entryFile: "index.js"
]

// configure env files for different build flavors.
// NOTE: use all lowercase
// https://github.com/luggit/react-native-config#android-1
project.ext.envConfigFiles = [
  stagingdebug: ".env.staging",
  stagingrelease: ".env.staging",
  proddebug: ".env.prod",
  prodrelease: ".env.prod"
]

apply from: "../../node_modules/react-native/react.gradle"
apply from: "../../node_modules/react-native-code-push/android/codepush.gradle"
apply from: project(':react-native-config').projectDir.getPath() + "/dotenv.gradle"

/**
 * Set this to true to create two separate APKs instead of one:
 *   - An APK that only works on ARM devices
 *   - An APK that only works on x86 devices
 * The advantage is the size of the APK is reduced by about 4MB.
 * Upload all the APKs to the Play Store and people will download
 * the correct one based on the CPU architecture of their device.
 */
def enableSeparateBuildPerCPUArchitecture = true

/**
 * Run Proguard to shrink the Java bytecode in release builds.
 */
def enableProguardInReleaseBuilds = false


def isAppCenterBuild = System.getenv("APPCENTER_BRANCH")

// output values from .env file so we know it's getting picked up correctly
System.println("*** HOST: " + project.env.get("HOST"))
System.println("*** IS_STAGING: " + project.env.get("IS_STAGING"))

android {
  lintOptions {
    disable 'GradleCompatible'
    disable 'InvalidPackage'
  }

  // because of firestore:
  dexOptions {
    javaMaxHeapSize "4g"
  }
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  flavorDimensions "default"

  defaultConfig {
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion

    def versionPropsFile = file('version.properties')
    Properties versionProps = new Properties()

    if (versionPropsFile.canRead()) {
        versionProps.load(new FileInputStream(versionPropsFile))
    }
    // def code = versionProps['VERSION_CODE'].toInteger()
    def name= versionProps['VERSION_NAME']

    // this gets bumped automatically by AppCenter
    versionCode 2

    // this should match the versions in package.json, ios, etc
    versionName name

    ndk {
      // abiFilters "armeabi-v7a", "x86", "arm64-v8a", "x86_64"
    }
    // because of firestore:
    multiDexEnabled true

    // https://github.com/luggit/react-native-config#advanced-android-setup
    resValue "string", "build_config_package", "com.hippware.android.tinyrobot"

    // for rn-image-crop-picker
    vectorDrawables.useSupportLibrary = true
  }
  signingConfigs {
    // since AppCenter signs the apk separately don't specify signing configs there
    if (!isAppCenterBuild) {
      staging {
        storeFile file(STAGING_RELEASE_STORE_FILE)
        storePassword STAGING_RELEASE_STORE_PASSWORD
        keyAlias STAGING_RELEASE_KEY_ALIAS
        keyPassword STAGING_RELEASE_KEY_PASSWORD
      }
      prod {
        storeFile file(PROD_RELEASE_STORE_FILE)
        storePassword PROD_RELEASE_STORE_PASSWORD
        keyAlias PROD_RELEASE_KEY_ALIAS
        keyPassword PROD_RELEASE_KEY_PASSWORD
      }
    }
  }
  splits {
    abi {
      // https://developer.android.com/studio/build/configure-apk-splits.html
      // https://proandroiddev.com/reducing-apk-size-by-using-abi-filters-and-apk-split-74a68a885f4e

      enable enableSeparateBuildPerCPUArchitecture

      // By default all ABIs are included, so use reset() and include to specify that we only
      // want APKs for x86, armeabi-v7a, and mips.
      reset()
      
      // Specifies that we want to also generate a universal APK that includes all ABIs.
      universalApk false  // If true, also generate a universal APK

      // Specifies a list of ABIs that Gradle should create APKs for.
      include "armeabi-v7a", "x86", "arm64-v8a", "x86_64"
    }
  }
  buildTypes {
    release {
      minifyEnabled enableProguardInReleaseBuilds
      proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
    }
    debug {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  // todo: flesh out flavors
  // https://medium.com/@ywongcode/building-multiple-versions-of-a-react-native-app-4361252ddde5
  // https://developer.android.com/studio/build/build-variants
  productFlavors {
    staging {
      minSdkVersion rootProject.ext.minSdkVersion
      targetSdkVersion rootProject.ext.targetSdkVersion

      // don't sign AppCenter gradle builds...the APK is signed in a subsequent build step.
      if (!isAppCenterBuild) {
        signingConfig signingConfigs.staging
      }
      applicationIdSuffix ".staging"
    }
    prod {
      minSdkVersion rootProject.ext.minSdkVersion
      targetSdkVersion rootProject.ext.targetSdkVersion
      if (!isAppCenterBuild) {
        signingConfig signingConfigs.prod
      }
    }
  }

  // applicationVariants are e.g. debug, release
  applicationVariants.all { variant ->
    variant.outputs.each { output ->
      // For each separate APK per architecture, set a unique version code as described here:
      // https://developer.android.com/studio/build/configure-apk-splits.html#configure-APK-versions
      // Also see https://developer.android.com/google/play/publishing/multiple-apks.html#using-a-version-code-scheme
      def versionCodes = ["armeabi-v7a": 1, "x86": 2, "arm64-v8a": 3, "x86_64": 4]
      def abi = output.getFilter(OutputFile.ABI)
      if (abi != null) {  // null for the universal-debug, universal-release variants
        output.versionCodeOverride =
          versionCodes.get(abi) * 1000000 + defaultConfig.versionCode
      }
    }
  }
}

dependencies {
  implementation project(':react-native-splash-screen')
  implementation project(':react-native-pure-jwt')
  implementation project(':react-native-action-sheet')
  implementation project(':react-native-image-crop-picker')
  implementation project(':react-native-android-open-settings')
  implementation project(':react-native-extra-dimensions-android')
  implementation project(':react-native-localize')
  implementation project(':react-native-gesture-handler')
  implementation 'com.android.support:multidex:1.0.3'
  implementation project(':react-native-background-geolocation')
  implementation project(':bugsnag-react-native')
  implementation(project(':react-native-push-notification')){
    exclude group: 'com.google.firebase', module: 'firebase-iid'
    exclude group: 'com.google.firebase', module: 'firebase-messaging'
  }
  implementation project(':react-native-linear-gradient')
  implementation project(':react-native-fs')
  implementation project(':react-native-touch-through-view')
  implementation project(':react-native-mixpanel')
  implementation project(':react-native-device-info')
  implementation project(':react-native-code-push')
  implementation(project(':react-native-maps')){
    exclude group: 'com.google.android.gms', module: 'play-services-base'
    exclude group: 'com.google.android.gms', module: 'play-services-maps'
  } 
  implementation project(':react-native-config')
  implementation project(':react-native-contacts')
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native:+"
  implementation fileTree(dir: "libs", include: ["*.jar"])

  /* ----------------------------
   *    REACT NATIVE FIREBASE
   * ---------------------------- */

  // Library as defined in settings.gradle
  implementation project(path: ':react-native-firebase', configuration: 'default')
  // Required dependencies
  //noinspection GradleCompatible
  implementation "com.google.firebase:firebase-core:16.0.6"
  implementation "com.google.android.gms:play-services-base:16.0.1"
  implementation "com.google.android.gms:play-services-maps:16.0.0"

  /* -------------------------
   *   OPTIONAL FIREBASE SDKS
   * ------------------------- */

  // Authentication
  implementation "com.google.firebase:firebase-auth:16.1.0"
  // Dynamic Links
  implementation "com.google.firebase:firebase-dynamic-links:16.1.5"
  // Cloud Messaging / FCM
  implementation ("com.google.firebase:firebase-messaging:17.3.4"){
    exclude group: 'com.google.firebase', module: 'firebase-iid'
  }

  // For React Native Firebase Notifications
  implementation 'me.leolin:ShortcutBadger:1.1.21@aar'

  // for Android animated gif support
  implementation 'com.facebook.fresco:fresco:1.10.0'
  implementation 'com.facebook.fresco:animated-gif:1.10.0'
}

// Run this once to be able to run the application with BUCK
// puts all compile dependencies into folder libs for BUCK to use
task copyDownloadableDepsToLibs(type: Copy) {
  from configurations.compile
  into 'libs'
}

apply plugin: 'com.google.gms.google-services'
